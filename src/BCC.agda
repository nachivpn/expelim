{-# OPTIONS --postfix-projections #-}
------------------------------------------------------------------------
-- Term language of BCC combinators
--
-- Term syntax, typing, conversion rules and derived laws
------------------------------------------------------------------------

module BCC where

open import Type
open import Util

infixr 4 _âˆ˜_
infixr 3 <_,_>

data BCC : (a b : Ty) â†’ Set where

  -- Category morphisms
  id    : âˆ€ {a}     â†’ BCC a a
  _âˆ˜_   : âˆ€ {a b c} â†’ BCC b c â†’ BCC a b â†’ BCC a c

  -- Product morphisms
  Ï€â‚    : âˆ€ {a b}   â†’ BCC (a * b) a
  Ï€â‚‚    : âˆ€ {a b}   â†’ BCC (a * b) b
  <_,_> : âˆ€ {a b c} â†’ BCC a b â†’ BCC a c â†’ BCC a (b * c)

  -- Initial morphism
  init  : âˆ€ {a}     â†’ BCC ğŸ˜ a

  -- Terminal morphism
  unit  : âˆ€ {a}     â†’ BCC a ğŸ™

  -- co-product morphisms
  injl  : âˆ€ {a b}     â†’ BCC a (a + b)
  injr  : âˆ€ {a b}     â†’ BCC b (a + b)
  [_,_] : âˆ€ {a b c} â†’ BCC a c â†’ BCC b c â†’ BCC (a + b) c

  -- Exponential morphisms
  curry : âˆ€ {a b c} â†’ BCC (c * a) b â†’ BCC c (a â‡’ b)
  apply : âˆ€ {a b}  â†’ BCC ((a â‡’ b) * a) b


_âŠ—_ : âˆ€{a b c d} â†’ BCC a b â†’ BCC c d â†’ BCC (a * c) (b * d)
f âŠ— g = < (f âˆ˜ Ï€â‚) , (g âˆ˜ Ï€â‚‚) >

uncurry : âˆ€{c a b} â†’ BCC c (a â‡’ b) â†’ BCC (c * a) b
uncurry f = apply âˆ˜ (f âŠ— id)

-- | Distribute * over +
distrf : âˆ€ {a b c} â†’ BCC (a * (b + c)) ((a * b) + (a * c))
distrf = apply âˆ˜
    < [ curry (injl âˆ˜ < Ï€â‚‚ , Ï€â‚ >)
    , curry (injr âˆ˜ < Ï€â‚‚ , Ï€â‚ >) ] âˆ˜ Ï€â‚‚
    , Ï€â‚ >

-- | Distribute * over + (reverse)
distrb : âˆ€ {a b c} â†’ BCC ((a * b) + (a * c)) (a * (b + c))
distrb = [ < Ï€â‚ , injl âˆ˜ Ï€â‚‚ > , < Ï€â‚ , injr âˆ˜ Ï€â‚‚ > ]

copair : âˆ€ {a b c d} â†’ BCC (a * b) d -> BCC (a * c) d -> BCC (a * (b + c)) d
copair f g = [ f , g ] âˆ˜ distrf

caseM : âˆ€ {a b c d} â†’ BCC a (b + c) â†’ BCC (a * b) d -> BCC (a * c) d -> BCC a d
caseM x f g = copair f g âˆ˜ < id , x >

infix 2 _â‰ˆ_

data _â‰ˆ_ : âˆ€ {a b} â†’ (f g : BCC a b) â†’ Set where

  -- categorical laws
  
  idr  : âˆ€ {a b} {f : BCC a b}
    â†’ (f âˆ˜ id) â‰ˆ f
  idl  : âˆ€ {a b} {f : BCC a b}
    â†’ (id âˆ˜ f) â‰ˆ f
  assoc : âˆ€ {a b c d} {h : BCC a b} {g : BCC b c} {f : BCC c d}
    â†’ f âˆ˜ (g âˆ˜ h) â‰ˆ (f âˆ˜ g) âˆ˜ h

  -- elimination laws

  Ï€â‚-pair : âˆ€{a b c} {f : BCC c a} {g : BCC c b}
    â†’ (Ï€â‚ âˆ˜ < f , g >) â‰ˆ f
  Ï€â‚‚-pair : âˆ€{a b c} {f : BCC c a} {g : BCC c b}
    â†’ (Ï€â‚‚ âˆ˜ < f , g >) â‰ˆ g
  match-injl : âˆ€{a b c} {f : BCC a c} {g : BCC b c}
    â†’ ([ f , g ] âˆ˜ injl) â‰ˆ f
  match-injr : âˆ€{a b c} {f : BCC a c} {g : BCC b c}
    â†’ ([ f , g ] âˆ˜ injr) â‰ˆ g
  apply-curry : âˆ€ {a b c} {f : BCC (a * b) c}
    â†’ (apply âˆ˜ (curry f âŠ— id)) â‰ˆ f

  -- uniqueness laws
  
  uniq-init : âˆ€ {a} {f : BCC ğŸ˜ a}
    â†’ init â‰ˆ f
  uniq-unit : âˆ€ {a} {f : BCC a ğŸ™}
    â†’ unit â‰ˆ f
  uniq-pair  : âˆ€ {a b z f g} {h : BCC z (a * b)}
    â†’ (Ï€â‚ âˆ˜ h) â‰ˆ f
    â†’ (Ï€â‚‚ âˆ˜ h) â‰ˆ g
    â†’ < f , g > â‰ˆ h
  uniq-curry : âˆ€ {a b c} {h : BCC a (b â‡’ c)} {f : BCC (a * b) c}
    â†’ apply âˆ˜ (h âŠ— id) â‰ˆ f
    â†’ curry f â‰ˆ h
  uniq-match : âˆ€ {a b z f g} {h : BCC (a + b) z}
    â†’ (h âˆ˜ injl) â‰ˆ f
    â†’ (h âˆ˜ injr) â‰ˆ g
    â†’ [ f , g ] â‰ˆ h
    
  -- _â‰ˆ_ is an equivalence relation
  
  refl  : âˆ€ {a b} {f : BCC a b}
    â†’ f â‰ˆ f
  sym   : âˆ€ {a b} {f g : BCC a b}
    â†’ f â‰ˆ g â†’ g â‰ˆ f
  trans : âˆ€ {a b} {f g h : BCC a b}
    â†’ f â‰ˆ g â†’ g â‰ˆ h â†’ f â‰ˆ h

  -- congruence laws
  
  congl : âˆ€ {a b c} {x y : BCC a b} {f : BCC b c}
    â†’ x â‰ˆ y â†’ f âˆ˜ x â‰ˆ f âˆ˜ y
  congr : âˆ€ {a b c} {x y : BCC b c} {f : BCC a b}
    â†’ x â‰ˆ y â†’ x âˆ˜ f â‰ˆ y âˆ˜ f

-- Distributivity laws hold in any BCC 
-- Distributivity laws can be derived in _â‰ˆ_, but the reasoning
-- is quite standard and boring, so we add it as a postulate here.
postulate
  distrfnb : âˆ€{a b c} â†’ distrb âˆ˜ distrf â‰ˆ id {a * (b + c)}
  -- reverse also holds, but not needed here

-- boilerplate to use the setoid library for eq. reasoning
module SetoidUtil where

  open import Relation.Binary
    using (Setoid ; IsEquivalence) 

  open Setoid
    renaming (_â‰ˆ_ to _â‰ˆâ‚‘_)
    using (Carrier ; isEquivalence)

  Hom : (a b : Ty) â†’ Setoid _ _
  Hom a b .Carrier = BCC a b
  Hom a b ._â‰ˆâ‚‘_     = _â‰ˆ_
  Hom a b .isEquivalence .IsEquivalence.refl  = refl
  Hom a b .isEquivalence .IsEquivalence.sym   = _â‰ˆ_.sym
  Hom a b .isEquivalence .IsEquivalence.trans = _â‰ˆ_.trans

  import Relation.Binary.SetoidReasoning as SetoidR
  open SetoidR public

open SetoidUtil 

-- standard pair rules

comp-pair : âˆ€{a b c d} {h : BCC d c} {f : BCC c a} {g : BCC c b}
  â†’ < f , g > âˆ˜ h â‰ˆ < (f âˆ˜ h) , (g âˆ˜ h) >
comp-pair = sym
  (uniq-pair
    (trans assoc (congr Ï€â‚-pair))
    (trans assoc (congr Ï€â‚‚-pair)))

cong-pair : âˆ€{a b d} â†’ {f f' : BCC a b} {g g' : BCC a d}
  â†’ f â‰ˆ f' â†’ g â‰ˆ g' â†’ < f , g > â‰ˆ < f' , g' >
cong-pair p q = uniq-pair
  (trans Ï€â‚-pair (sym p))
  (trans Ï€â‚‚-pair (sym q))

-- standard âˆ˜ rules

cong-âˆ˜ : âˆ€{a b c} â†’ {f f' : BCC a b} {g g' : BCC c a}
  â†’ f â‰ˆ f' â†’ g â‰ˆ g' â†’ f âˆ˜ g â‰ˆ f' âˆ˜ g'
cong-âˆ˜ p q = trans (congr p) (congl q)

-- standard âŠ— rules

cong-âŠ— : âˆ€{a b c d} â†’ {f f' : BCC a b} {g g' : BCC c d}
  â†’ f â‰ˆ f' â†’ g â‰ˆ g' â†’ f âŠ— g â‰ˆ f' âŠ— g'
cong-âŠ— p q = uniq-pair
  (trans Ï€â‚-pair (sym (congr p)))
  (trans Ï€â‚‚-pair (sym (congr q)))

comp-âŠ— : âˆ€{a b c d} {h : BCC d c} {f : BCC c b}
  â†’ (f âˆ˜ h) âŠ— id {a} â‰ˆ (f âŠ— id) âˆ˜ (h âŠ— id)
comp-âŠ— = sym
  (trans
    comp-pair
    (cong-pair
      (trans
        (sym assoc) (trans (congl Ï€â‚-pair) assoc))
        (trans (sym assoc) (congl (trans Ï€â‚‚-pair idl)))))

-- standard curry rules

cong-curry : âˆ€{a b c} â†’ {f f' : BCC (a * b) c}
  â†’ f â‰ˆ f' â†’ curry f â‰ˆ curry f'
cong-curry p = uniq-curry (trans apply-curry (sym p))

comp-curry : âˆ€{a b c d} {h : BCC d c} {f : BCC (c * a) b}
  â†’ curry f âˆ˜ h â‰ˆ curry (f âˆ˜ (h âŠ— id))
comp-curry = sym (uniq-curry
  (trans
    (congl comp-âŠ—)
    (trans assoc (congr apply-curry))))

-- standard match rules

cong-match : âˆ€ {a b c} {f f' : BCC a c} {g g' : BCC b c}
  â†’ f â‰ˆ f'
  â†’ g â‰ˆ g'
  â†’ ([ f , g ]) â‰ˆ ([ f' , g' ])
cong-match p q = uniq-match
  (trans match-injl (sym p))
  (trans match-injr (sym q))  

comp-match : âˆ€ {a b c d} {h : BCC c d} {f  : BCC a c} {g : BCC b c}
  â†’ h âˆ˜ [ f , g ] â‰ˆ [ h âˆ˜ f , h âˆ˜ g ]
comp-match = sym (uniq-match
  (trans (sym assoc) (congl match-injl))
  ((trans (sym assoc) (congl match-injr))))

-- beta reduction (useful lemma)

Î²â‡’  : âˆ€ {a b c} (f : BCC (c * a) b) (g : BCC c a)
  â†’ apply âˆ˜ < (curry f) , g > â‰ˆ (f âˆ˜ < id , g >)
Î²â‡’ f g = trans
  (trans
    (congl
      (sym (trans
        comp-pair
        (cong-pair
          (trans (sym assoc) (trans (congl Ï€â‚-pair) idr))
          (trans (sym assoc) (trans idl Ï€â‚‚-pair))))))
    assoc)
  (congr apply-curry)

-- copair laws (useful intermediate step to prove special rules about case)

copair-injl : âˆ€{a b c d} {f : BCC (a * b) d} {g : BCC (a * c) d}
    â†’ copair f g âˆ˜ (id âŠ— injl) â‰ˆ  f
copair-injl {f = f} {g} = beginâŸ¨ Hom _ _ âŸ©
  copair f g âˆ˜ (< id âˆ˜ Ï€â‚ , injl âˆ˜ Ï€â‚‚ >)
    â‰ˆâŸ¨ sym assoc âŸ©
  [ f , g ] âˆ˜ (distrf âˆ˜ < id âˆ˜ Ï€â‚ , injl âˆ˜ Ï€â‚‚ >)
    â‰ˆâŸ¨ congl (trans
       (sym assoc)
       (congl (trans
         comp-pair
         (cong-pair
           (trans (sym assoc) (trans (congl Ï€â‚‚-pair) (trans assoc (congr match-injl))))
           (trans Ï€â‚-pair idl))))) âŸ©
  [ f , g ] âˆ˜ apply âˆ˜ < curry (injl âˆ˜ < Ï€â‚‚ , Ï€â‚ >) âˆ˜ Ï€â‚‚ , Ï€â‚ >
      â‰ˆâŸ¨ congl (congl (cong-pair
         (trans
           comp-curry (cong-curry (trans (sym assoc)
           (congl (congl (cong-pair refl idl))))))
         refl)) âŸ©
  [ f , g ] âˆ˜ apply âˆ˜ < curry (injl âˆ˜ < Ï€â‚‚ , Ï€â‚ > âˆ˜ < Ï€â‚‚ âˆ˜ Ï€â‚ , Ï€â‚‚ >) , Ï€â‚ >
    â‰ˆâŸ¨ congl (congl (cong-pair (cong-curry
      (congl (trans comp-pair (cong-pair Ï€â‚‚-pair Ï€â‚-pair))))
      refl)) âŸ©
  [ f , g ] âˆ˜ apply âˆ˜ < curry (injl âˆ˜ < Ï€â‚‚ , Ï€â‚‚ âˆ˜ Ï€â‚ >) , Ï€â‚ >
    â‰ˆâŸ¨ congl (trans (Î²â‡’ _ _) (sym assoc)) âŸ©
  [ f , g ] âˆ˜ injl âˆ˜ (< Ï€â‚‚ , Ï€â‚‚ âˆ˜ Ï€â‚ > âˆ˜ < id , Ï€â‚ >)
      â‰ˆâŸ¨ congl (congl (trans
          comp-pair
          (cong-pair Ï€â‚‚-pair (trans
            (sym assoc)
            (trans (congl Ï€â‚-pair) idr))))) âŸ©
  [ f , g ] âˆ˜ injl âˆ˜ (< Ï€â‚ , Ï€â‚‚ >)
    â‰ˆâŸ¨ congl (trans (congl (uniq-pair idr idr)) idr) âŸ©
  [ f , g ] âˆ˜ injl
    â‰ˆâŸ¨ match-injl âŸ©
  f âˆ    
               
copair-injr : âˆ€{a b c d} {f : BCC (a * b) d} {g : BCC (a * c) d}
    â†’ copair f g âˆ˜ (id âŠ— injr) â‰ˆ g
-- symmetric to proof steps in copair-injl
copair-injr {f = f} {g} = beginâŸ¨ Hom _ _ âŸ©
  copair f g âˆ˜ (< id âˆ˜ Ï€â‚ , injr âˆ˜ Ï€â‚‚ >)
    â‰ˆâŸ¨ sym assoc âŸ©
  [ f , g ] âˆ˜ (distrf âˆ˜ < id âˆ˜ Ï€â‚ , injr âˆ˜ Ï€â‚‚ >)
      â‰ˆâŸ¨ congl (trans
       (sym assoc)
       (congl (trans
         comp-pair
         (cong-pair
           (trans (sym assoc) (trans (congl Ï€â‚‚-pair) (trans assoc (congr match-injr))))
           (trans Ï€â‚-pair idl))))) âŸ©
   [ f , g ] âˆ˜ apply âˆ˜ < curry (injr âˆ˜ < Ï€â‚‚ , Ï€â‚ >) âˆ˜ Ï€â‚‚ , Ï€â‚ >
       â‰ˆâŸ¨ congl (congl (cong-pair
         (trans
           comp-curry (cong-curry (trans (sym assoc)
           (congl (congl (cong-pair refl idl))))))
         refl)) âŸ©
  [ f , g ] âˆ˜ apply âˆ˜ < curry (injr âˆ˜ < Ï€â‚‚ , Ï€â‚ > âˆ˜ < Ï€â‚‚ âˆ˜ Ï€â‚ , Ï€â‚‚ >) , Ï€â‚ >
      â‰ˆâŸ¨ congl (congl (cong-pair (cong-curry
        (congl (trans comp-pair (cong-pair Ï€â‚‚-pair Ï€â‚-pair))))
        refl)) âŸ©
  [ f , g ] âˆ˜ apply âˆ˜ < curry (injr âˆ˜ < Ï€â‚‚ , Ï€â‚‚ âˆ˜ Ï€â‚ >) , Ï€â‚ >
      â‰ˆâŸ¨ congl (trans (Î²â‡’ _ _) (sym assoc)) âŸ©
  [ f , g ] âˆ˜ injr âˆ˜ (< Ï€â‚‚ , Ï€â‚‚ âˆ˜ Ï€â‚ > âˆ˜ < id , Ï€â‚ >)
      â‰ˆâŸ¨ congl (congl (trans
          comp-pair
          (cong-pair Ï€â‚‚-pair (trans
            (sym assoc)
            (trans (congl Ï€â‚-pair) idr))))) âŸ©
  [ f , g ] âˆ˜ injr âˆ˜ (< Ï€â‚ , Ï€â‚‚ >)
      â‰ˆâŸ¨ congl (trans (congl (uniq-pair idr idr)) idr) âŸ©
  [ f , g ] âˆ˜ injr
      â‰ˆâŸ¨ match-injr âŸ©
  g âˆ

uniq-copair : âˆ€ {a b c d} {f : BCC (a * b) d} {g : BCC (a * c) d} {h : BCC (a * (b + c)) d}
    â†’ f â‰ˆ h âˆ˜ (id âŠ— injl)
    â†’ g â‰ˆ h âˆ˜ (id âŠ— injr)
    â†’ copair f g â‰ˆ h
uniq-copair p q = trans
  (congr (trans (cong-match p q) (sym comp-match)))
  (trans
    (sym assoc)
    (trans
      (congl (trans
        (congr (cong-match (cong-pair idl refl) (cong-pair idl refl)))
        distrfnb))
      idr))

-- standard copair laws

cong-copair : âˆ€ {a b c d} {f f' : BCC (a * b) d} {g g' : BCC (a * c) d}
  â†’ f â‰ˆ f'
  â†’ g â‰ˆ g'
  â†’ copair f g â‰ˆ copair f' g'
cong-copair p q = uniq-copair
  (trans p (sym copair-injl))
  (trans q (sym copair-injr))  

comp-copair : âˆ€ {a b c d e} {f : BCC (a * b) d} {g : BCC (a * c) d} {h : BCC d e}
  â†’ h âˆ˜ copair f g â‰ˆ copair (h âˆ˜ f) (h âˆ˜ g)
comp-copair = sym (uniq-copair
  (sym (trans (sym assoc) (congl copair-injl)))
  (sym (trans (sym assoc) (congl copair-injr))))

-- special copair law

post-comp-copair : âˆ€ {a b c d e} {f : BCC (a * b) d}
  {g : BCC (a * c) d} {h : BCC e a}
  â†’ copair f g âˆ˜ (h âŠ— id) â‰ˆ copair (f âˆ˜ h âŠ— id) (g âˆ˜ h âŠ— id)
post-comp-copair {f = f} {g} {h} = beginâŸ¨ Hom _ _ âŸ©
  copair f g âˆ˜ (h âŠ— id)
    â‰ˆâŸ¨ sym (uniq-copair refl refl) âŸ©
  copair
    ((copair f g âˆ˜ (h âŠ— id)) âˆ˜ (id âŠ— injl))
    ((copair f g âˆ˜ (h âŠ— id)) âˆ˜ (id âŠ— injr))
    â‰ˆâŸ¨ cong-copair
      (trans (sym assoc) (trans (congl swap-âŠ—) assoc))
      (trans (sym assoc) (trans (congl swap-âŠ—) assoc)) âŸ©
  copair
    ((copair f g âˆ˜ (id âŠ— injl)) âˆ˜ (h âŠ— id))
    ((copair f g âˆ˜ (id âŠ— injr)) âˆ˜ (h âŠ— id))
    â‰ˆâŸ¨ cong-copair (congr copair-injl) (congr copair-injr) âŸ©
  copair
    (f âˆ˜ h âŠ— id)
    (g âˆ˜ h âŠ— id) âˆ
    where
      swap-âŠ— : âˆ€{a b c d} {f : BCC a b} {g : BCC c d}
        â†’ (f âŠ— id) âˆ˜ (id âŠ— g) â‰ˆ (id âŠ— g) âˆ˜ (f âŠ— id)
      swap-âŠ— = trans
        comp-pair                    -- merge pairs on left
        (sym (trans comp-pair  -- merge pairs on right
          (cong-pair
            -- simplify the first components on both sides till they're equal
            (trans
              (congr idl)
              (trans
                Ï€â‚-pair
                (sym (trans (sym assoc) (congl (trans Ï€â‚-pair idl))))))
            -- simplify the second component on both sides till they're equal
            (trans
              (sym assoc)
              (trans
                (congl (trans Ï€â‚‚-pair idl))
                (sym (trans (congr idl) Ï€â‚‚-pair)))))))

-- standard case rules

cong-caseM : âˆ€ {a b c d}
  {x x' :  BCC a (b + c)} {f f' : BCC (a * b) d} {g g' : BCC (a * c) d}
  â†’ x â‰ˆ x'
  â†’ f â‰ˆ f'
  â†’ g â‰ˆ g'
  â†’ caseM x f g â‰ˆ caseM x' f' g'
cong-caseM p q r = cong-âˆ˜ (cong-copair q r) (cong-pair refl p)

comp-caseM : âˆ€ {a b c d e} {h : BCC d e}
  {x :  BCC a (b + c)} {f  : BCC (a * b) d} {g : BCC (a * c) d}
  â†’ h âˆ˜ (caseM x f g) â‰ˆ caseM x (h âˆ˜ f) (h âˆ˜ g)
comp-caseM = trans assoc (congr comp-copair)

-- eta rules

Î·â‡’ : âˆ€{a b c} â†’ {f : BCC a (b â‡’ c)}
  â†’ f â‰ˆ curry (uncurry f)
Î·â‡’ = sym (uniq-curry refl)

Î·* : âˆ€{a b c} â†’ {f : BCC a (b * c)}
  â†’ f â‰ˆ < Ï€â‚ âˆ˜ f , Ï€â‚‚ âˆ˜ f >
Î·* = sym (uniq-pair refl refl)

Î·+ : âˆ€{a b c} â†’ {f : BCC a (b + c)}
  â†’ f â‰ˆ caseM f (injl âˆ˜ Ï€â‚‚) (injr âˆ˜ Ï€â‚‚)
Î·+ = sym
  (trans
    (congr
      (uniq-copair
        (sym Ï€â‚‚-pair)
        (sym Ï€â‚‚-pair)))
    Ï€â‚‚-pair)

-- special case rules

post-comp-caseM : âˆ€ {a b c d e} {h : BCC e a}
  {x :  BCC a (b + c)} {f  : BCC (a * b) d} {g : BCC (a * c) d}
  â†’ (caseM x f g) âˆ˜ h â‰ˆ caseM (x âˆ˜ h) (f âˆ˜ h âŠ— id {b}) (g âˆ˜ h âŠ— id)
post-comp-caseM {h = h} {x} {f} {g} = beginâŸ¨ Hom _ _ âŸ©
  (caseM x f g) âˆ˜ h
    â‰ˆâŸ¨ sym assoc âŸ©
  copair f g âˆ˜ < id , x > âˆ˜ h
    â‰ˆâŸ¨ congl comp-pair âŸ©
  copair f g âˆ˜ < id âˆ˜ h , x âˆ˜ h >
    â‰ˆâŸ¨ congl (cong-pair idl refl) âŸ©
  copair f g âˆ˜ < h , x âˆ˜ h >
    â‰ˆâŸ¨ congl (trans
       (cong-pair
         (sym (trans (sym assoc) (trans (congl Ï€â‚-pair) idr)))
         (sym Ï€â‚‚-pair))
       (sym (comp-pair {h = < id , x âˆ˜ h >}  {f = h âˆ˜ Ï€â‚} {g = Ï€â‚‚}))) âŸ©
  copair f g âˆ˜ < h âˆ˜ Ï€â‚ , Ï€â‚‚ > âˆ˜  < id , x âˆ˜ h > 
    â‰ˆâŸ¨ assoc âŸ©
  (copair f g âˆ˜ < h âˆ˜ Ï€â‚ , Ï€â‚‚ >) âˆ˜ < id , x âˆ˜ h > 
    â‰ˆâŸ¨ congr (sym (uniq-copair
      (sym (trans
        (congr
          (trans
            (congl (cong-pair refl (sym idl)))
            post-comp-copair))
         copair-injl))
       (sym (trans
        (congr
          (trans
            (congl (cong-pair refl (sym idl)))
            post-comp-copair))
         copair-injr)))) âŸ©
  copair (f âˆ˜ h âŠ— id) (g âˆ˜ h âŠ— id) âˆ˜ < id , x âˆ˜ h >
    â‰ˆâŸ¨ refl âŸ©
  caseM (x âˆ˜ h) (f âˆ˜ h âŠ— id) (g âˆ˜ h âŠ— id) âˆ    


apply-case : âˆ€{a b c d e}
  {x : BCC a (b + c)} {y : BCC a d}
  {f : BCC (a * b) (d â‡’ e)} {g : BCC (a * c) (d â‡’ e)} 
  â†’ apply âˆ˜ < caseM x f g , y > â‰ˆ
    caseM x
      (apply âˆ˜ < f , y âˆ˜ Ï€â‚ >)
      (apply âˆ˜ < g , y âˆ˜ Ï€â‚ >)
apply-case = sym (trans (sym comp-caseM) (congl (trans Î·*
  (cong-pair
    (trans comp-caseM (cong-caseM refl Ï€â‚-pair Ï€â‚-pair))
    (trans
      comp-caseM
      (trans
        (cong-caseM refl Ï€â‚‚-pair Ï€â‚‚-pair)
        (trans
          (sym comp-caseM)
          (trans (congl useless-case) idr))))))))
  where
  useless-case : âˆ€{a b c} {x : BCC a (b + c)} â†’ caseM x Ï€â‚ Ï€â‚ â‰ˆ id {a}
  useless-case =
    trans
      (congr
        (uniq-copair
          (sym (trans Ï€â‚-pair idl))
          (sym (trans Ï€â‚-pair idl))))
       Ï€â‚-pair



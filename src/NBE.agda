open import Type
open import Util
open import BCC
open import Sel
open import Presheaf
open import Data.Unit using (tt)
open import Data.Sum using (injâ‚ ; injâ‚‚)

-- neutral and normal forms
mutual

  data Ne (a : Ty) : Ty â†’ Set where
    sel : âˆ€ {b}   â†’ Sel a b      â†’ Ne a b           -- id / Ï€â‚ / < , >        
    fst : âˆ€ {b c} â†’ Ne a (b * c) â†’ Ne a b           -- Ï€â‚ âˆ˜ x
    snd : âˆ€ {b c} â†’ Ne a (b * c) â†’ Ne a c           -- Ï€â‚‚ âˆ˜ x
    app : âˆ€ {b c} â†’ Ne a (b â‡’ c) â†’ Nf a b â†’ Ne a c  -- eval âˆ˜ < x , n >
        
  data Nf (a : Ty) : Ty â†’ Set where
    unit :             Nf a ğŸ™
    ne-ğ•“ :             Ne a ğ•“ â†’ Nf a ğ•“
    ne-âŠ¥ : âˆ€ {b}     â†’ Ne a ğŸ˜ â†’ Nf a b
    injl : âˆ€ {b c}   â†’ Nf a b â†’ Nf a (b + c)
    injr : âˆ€ {b c}   â†’ Nf a c â†’ Nf a (b + c)
    pair : âˆ€ {b c}   â†’ Nf a b â†’ Nf a c â†’ Nf a (b * c)
    abs  : âˆ€ {b c}   â†’ Nf (a * b) c â†’ Nf a (b â‡’ c)
    case : âˆ€ {b c d} â†’ Ne a (b + c) â†’ Nf (a * b) d â†’ Nf (a * c) d â†’ Nf a d

mutual

  liftNf : âˆ€ {i j a} â†’ Sel j i â†’ Nf i a â†’ Nf j a
  liftNf e unit         = unit
  liftNf e (ne-ğ•“ x)     = ne-ğ•“ (liftNe e x)
  liftNf e (ne-âŠ¥ x)     = ne-âŠ¥ (liftNe e x)
  liftNf e (injl n)     = injl (liftNf e n)
  liftNf e (injr n)     = injr (liftNf e n)
  liftNf e (pair n nâ‚)  = pair (liftNf e n) (liftNf e nâ‚)
  liftNf e (abs n)      = abs (liftNf (keep e) n)
  liftNf e (case x p q) = case (liftNe e x) (liftNf (keep e) p) (liftNf (keep e) q)
  
  liftNe : âˆ€ {i j a} â†’ Sel j i â†’ Ne i a â†’ Ne j a
  liftNe e (sel x)   = sel (x âˆ™ e)
  liftNe e (fst x)   = fst (liftNe e x)
  liftNe e (snd x)   = snd (liftNe e x)
  liftNe e (app n x) = app (liftNe e n) (liftNf e x)

open ğ’«

liftBCC : âˆ€ {i j a} â†’ Sel j i â†’ BCC i a â†’ BCC j a
liftBCC e m = m âˆ˜ embToBCC e

BCC' : (a : Ty) â†’ ğ’«
BCC' a .In i = BCC i a
BCC' a .lift = liftBCC

Ne' : (a : Ty) â†’ ğ’«
Ne' a .In i = Ne i a
Ne' a .lift = liftNe

Nf' : (a : Ty) â†’ ğ’«
Nf' a .In i = Nf i a
Nf' a .lift = liftNf

-- the decision tree monad
module TreeMonad where

  data Tree (i : Ty) (A : ğ’«) : Set where
    leaf   : (x : A .In i) â†’  Tree i A
    dead   : Ne i ğŸ˜ â†’ Tree i A
    -- "semantic case"
    branch : âˆ€{c d} â†’ Ne i (c + d) â†’ Tree (i * c) A â†’  Tree (i * d) A â†’ Tree i A 

  liftTree : âˆ€ {A i j} â†’ Sel j i â†’ Tree i A  â†’ Tree j A
  liftTree {A} e (leaf x)       = leaf (lift A e x)
  liftTree     e (dead x)       = dead (liftNe e x)
  liftTree     e (branch x p q) =
    branch (liftNe e x)
      (liftTree (keep e) p)
      (liftTree (keep e) q)
  
  Tree' : (A : ğ’«) â†’ ğ’«
  Tree' A .In i  = Tree i A
  Tree' A .lift    = liftTree

  return : âˆ€ {A} â†’ A â†’Ì‡ Tree' A
  return = leaf
  
  map : âˆ€ {A B : ğ’«} â†’ (A â†’Ì‡ B) â†’ Tree' A â†’Ì‡ Tree' B
  map t (leaf x)       = leaf (t x)
  map t (dead x)       = dead x
  map t (branch x p q) = branch x (map t p) (map t q)

  join : âˆ€{A} â†’ Tree' (Tree' A) â†’Ì‡ Tree' A
  join (leaf x)       = x
  join (dead x)       = dead x
  join (branch x p q) = branch x (join p) (join q)

open TreeMonad

-- interpretation of types as presheaves
âŸ¦_âŸ§ : Ty â†’ ğ’«
âŸ¦    ğŸ˜   âŸ§ = Tree' ğŸ˜'
âŸ¦    ğŸ™   âŸ§ = ğŸ™'
âŸ¦    ğ•“   âŸ§ = Nf' ğ•“
âŸ¦ a â‡’ b âŸ§ = âŸ¦ a âŸ§ â‡’' âŸ¦ b âŸ§
âŸ¦ a * b âŸ§ = âŸ¦ a âŸ§ Ã—' âŸ¦ b âŸ§
âŸ¦ a + b âŸ§ = Tree' (âŸ¦ a âŸ§ +' âŸ¦ b âŸ§)

-- special tree operations
module TreeOps where

  runTreeNf : âˆ€ {a} â†’ Tree' (Nf' a) â†’Ì‡ Nf' a
  runTreeNf (leaf x)      = x
  runTreeNf (dead x)      = ne-âŠ¥ x
  runTreeNf (branch x p q) = case x (runTreeNf p) (runTreeNf q)

  mutual

    -- (Tree c âŸ¦_âŸ§) is an "applicative functor"
    apTree : âˆ€ {a b c} â†’ Tree c âŸ¦ a â‡’ b âŸ§ â†’ Tree c âŸ¦ a âŸ§ â†’ Tree c âŸ¦ b âŸ§
    apTree {A} {B} (leaf x)       c = leaf (x iden (runTree {A} c))
    apTree {A} {B} (dead x)       c = dead x
    apTree {A} {B} (branch x f g) c =
      branch x
        (apTree {A} {B} f (lift (Tree' âŸ¦ A âŸ§) (drop iden) c))
        (apTree {A} {B} g (lift (Tree' âŸ¦ A âŸ§) (drop iden) c))
  
    runTree : âˆ€ {a} â†’ Tree' âŸ¦ a âŸ§ â†’Ì‡ âŸ¦ a âŸ§
    runTree {ğŸ˜}     c = join c
    runTree {ğŸ™}     _ = tt
    runTree {ğ•“}     c = runTreeNf c
    runTree {A â‡’ B} c = Î» Ï„ a â†’ runTree {B} (apTree {A} {B} (liftTree Ï„ c) (leaf a))
    runTree {A * B} c = runTree {A} (map projâ‚ c) , runTree {B} (map projâ‚‚ c)
    runTree {A + B} c = join c

open TreeOps

cast : âˆ€ A â†’ ğŸ˜' â†’Ì‡ A
cast _ ()

match' : âˆ€{a b c} â†’ (âŸ¦ a âŸ§ â†’Ì‡ âŸ¦ c âŸ§) â†’ (âŸ¦ b âŸ§ â†’Ì‡ âŸ¦ c âŸ§) â†’ ((âŸ¦ a âŸ§ +' âŸ¦ b âŸ§) â†’Ì‡ âŸ¦ c âŸ§)
match' f g (injâ‚ x) = f x
match' f g (injâ‚‚ y) = g y
    
eval : âˆ€ {b a} â†’ BCC a b â†’ (âŸ¦ a âŸ§ â†’Ì‡ âŸ¦ b âŸ§)
eval id x                    = x
eval (t âˆ˜ s) x               = (eval t) (eval s x)
eval Ï€â‚ (p , _)              = p
eval Ï€â‚‚ (_ , q)              = q
eval < p , q > x             = eval p x , eval q x
eval {b} init x              = runTree {b} (map (cast âŸ¦ b âŸ§) x)
eval unit x                  = tt
eval {b} {a} (curry f) x     = Î» Ï„ y â†’ eval f (âŸ¦ a âŸ§ .lift Ï„ x , y)
eval apply (f , x)           = f iden x
eval injl x                  = leaf (injâ‚ x)
eval injr x                  = leaf (injâ‚‚ x)
eval {c} {a + b} [ p , q ] x =
  runTree {c}
    (map (match' {a} {b} {c} (eval p) (eval q)) x)

mutual

  reflect : âˆ€ (a : Ty) â†’ Ne' a â†’Ì‡ âŸ¦ a âŸ§
  reflect ğŸ˜ x       = dead x
  reflect ğŸ™ x       = tt
  reflect ğ•“ x       = ne-ğ•“ x
  reflect (a â‡’ b) f = Î» Ï„ x â†’ reflect b (app (liftNe Ï„ f) (reifyVal x))
  reflect (a * b) x = reflect a (fst x) , reflect b (snd x)
  reflect (a + b) x = branch x
    (leaf (injâ‚ (reflect a (snd (sel iden)))))
    (leaf (injâ‚‚ (reflect b (snd (sel iden)))))

  reifyVal : âˆ€ {a : Ty} â†’ âŸ¦ a âŸ§ â†’Ì‡ Nf' a
  reifyVal {ğŸ˜} t           = runTreeNf (map (cast (Nf' ğŸ˜)) t)
  reifyVal {ğŸ™} t           = unit
  reifyVal {ğ•“} t           = t
  reifyVal {A â‡’ Aâ‚} f      = abs (reifyVal (f (drop iden) (reflect A (snd (sel iden)))))
  reifyVal {T * A} (p , q) = pair (reifyVal p) (reifyVal q)
  reifyVal {A + B} t       = runTreeNf (map reifyValOr t)
  
  reifyValOr : âˆ€ {a b} â†’ (âŸ¦ a âŸ§ +' âŸ¦ b âŸ§) â†’Ì‡ Nf' (a + b)
  reifyValOr (injâ‚ x) = injl (reifyVal x)
  reifyValOr (injâ‚‚ y) = injr (reifyVal y)

reflectáµ¢ : âˆ€ (a : Ty) â†’ âŸ¦ a âŸ§ .In a
reflectáµ¢ A = reflect A (sel iden)

reify : âˆ€ {a b : Ty} â†’ (âŸ¦ a âŸ§ â†’Ì‡ âŸ¦ b âŸ§) â†’ Nf a b
reify {T} f = reifyVal (f (reflectáµ¢ T))
    
norm : âˆ€ {a : Ty} â†’ BCC' a â†’Ì‡ Nf' a
norm t = reify (eval t)

-- quotations
mutual

  qâ‚“ : âˆ€ {a} â†’ Ne' a â†’Ì‡ BCC' a
  qâ‚“ (sel x)   = liftBCC x id
  qâ‚“ (fst x)   = Ï€â‚ âˆ˜ qâ‚“ x
  qâ‚“ (snd x)   = Ï€â‚‚ âˆ˜ qâ‚“ x
  qâ‚“ (app x n) = apply âˆ˜ < qâ‚“ x , q n >

  q : âˆ€ {a} â†’ Nf' a â†’Ì‡ BCC' a
  q unit          = unit
  q (ne-ğ•“ x)      = qâ‚“ x
  q (ne-âŠ¥ x)      = init âˆ˜ qâ‚“ x
  q (injl n)      = injl âˆ˜ q n
  q (injr n)      = injr âˆ˜ q n
  q (pair n nâ‚)   = < q n , q nâ‚ >
  q (abs n)       = curry (q n)
  q (case x n nâ‚) = caseM (qâ‚“ x) (q n) (q nâ‚)

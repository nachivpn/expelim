module Sel where

open import Type
open import Util
open import BCC

--  Selections are the combinator-equivalent of variables
-- `Sel i j` to be read as a selection of j from i (or j âŠ‘ i)

data Sel : Ty â†’ Ty â†’ Set where
  endğŸ™  : Sel ğŸ™ ğŸ™
  endğ•“  : Sel ğ•“ ğ•“
  endğŸ˜  : Sel ğŸ˜ ğŸ˜
  endâ‡’  : âˆ€ {a b}   â†’ Sel (a â‡’ b) (a â‡’ b)
  end+  : âˆ€ {a b}   â†’ Sel (a + b) (a + b)
  drop  : âˆ€ {a b c} â†’ Sel a b â†’ Sel (a * c) b
  keep  : âˆ€ {a b c} â†’ Sel a b â†’ Sel (a * c) (b * c)

-- the identity selection for each type
iden : âˆ€ {a} â†’ Sel a a
iden {ğ•“}      = endğ•“
iden {ğŸ™}      = endğŸ™
iden {ğŸ˜}      = endğŸ˜
iden {a â‡’ aâ‚} = endâ‡’
iden {a * aâ‚} = keep iden
iden {a + aâ‚} = end+

-- selections compose
_âˆ™_ : âˆ€ {a b c} â†’ Sel b c â†’ Sel a b â†’ Sel a c
f      âˆ™ endğŸ™   = f
f      âˆ™ endğ•“   = f
f      âˆ™ endğŸ˜   = f
f      âˆ™ endâ‡’   = f
f      âˆ™ end+   = f
f      âˆ™ drop g = drop (f âˆ™ g)
drop f âˆ™ keep g = drop (f âˆ™ g)
keep f âˆ™ keep g = keep (f âˆ™ g)

-- selections can be embedded into terms
embSel : âˆ€ {a b} â†’ Sel a b â†’ BCC a b
embSel endğŸ™     = id
embSel endğ•“     = id
embSel endğŸ˜     = id
embSel endâ‡’     = id
embSel end+     = id
embSel (drop e) = embSel e âˆ˜ Ï€â‚
embSel (keep e) = < embSel e âˆ˜ Ï€â‚ , Ï€â‚‚ >

open import Relation.Binary.PropositionalEquality using (_â‰¡_ ; refl ; cong)

-- Category of Selections

sel-idl : âˆ€ {a b} {s : Sel a b} â†’ iden âˆ™ s â‰¡ s
sel-idl {s = endğŸ™}   = refl
sel-idl {s = endğ•“}   = refl
sel-idl {s = endğŸ˜}   = refl
sel-idl {s = endâ‡’}   = refl
sel-idl {s = end+}   = refl
sel-idl {s = drop s} = cong drop sel-idl
sel-idl {s = keep s} = cong keep sel-idl

sel-idr : âˆ€ {a b} {s : Sel a b} â†’ s âˆ™ iden â‰¡ s
sel-idr {s = endğŸ™}   = refl
sel-idr {s = endğ•“}   = refl
sel-idr {s = endğŸ˜}   = refl
sel-idr {s = endâ‡’}   = refl
sel-idr {s = end+}   = refl
sel-idr {s = drop s} = cong drop sel-idr
sel-idr {s = keep s} = cong keep sel-idr

sel-assoc :  âˆ€ {a b c d} {x : Sel c d} {y : Sel b c} {z : Sel a b}
  â†’ (x âˆ™ y) âˆ™ z â‰¡ x âˆ™ (y âˆ™ z)
sel-assoc {x = x}      {y}      {endğŸ™}  = refl
sel-assoc {x = x}      {y}      {endğ•“}  = refl
sel-assoc {x = x}      {y}      {endğŸ˜}  = refl
sel-assoc {x = x}      {y}      {endâ‡’}  = refl
sel-assoc {x = x}      {y}      {end+}  = refl
sel-assoc {x = x}      {y}      {drop z} = cong drop (sel-assoc {z = z})
sel-assoc {x = x}      {drop y} {keep z} = cong drop (sel-assoc {z = z})
sel-assoc {x = drop x} {keep y} {keep z} = cong drop (sel-assoc {z = z})
sel-assoc {x = keep x} {keep y} {keep z} = cong keep (sel-assoc {z = z})

-- identity is unique

uniq-iden : âˆ€ {a b} â†’ keep iden â‰¡ iden {a * b}
uniq-iden = refl
